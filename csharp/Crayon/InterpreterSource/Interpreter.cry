
function interpret(
		@type('Stack<StackFrame>') stack,
		@type('Array<int>') ops,
		@type('Array<Array<int>>') args,
		@type('Array<string>') stringArgs, 
		@type('Array<string>') identifiers, 
		@type('Dictionary<string, int>') inv_identifiers, 
		literalTable, 
		classTable, 
		classMethods, 
		specialCache, 
		globals, 
		primitiveMethodLookup) {
	@type('Stack<Value>') valueStack = StackFrame$stack.valueStack;
	@type('Dictionary<int, Value>') locals = StackFrame$stack.locals;
	@type('int') LENGTH_ID = $_dictionary_get(inv_identifiers, 'length', -999);
	@type('ProgramData') p = $_get_program_data();
	integerSwitches = ProgramData$p.integerSwitchLookups;
	stringSwitches = ProgramData$p.stringSwitchLookups;
	primitiveMethodLookup = ProgramData$p.primitiveMethodLookup;
	while (true) {
		row = args[StackFrame$stack.pc];
		switch (ops[StackFrame$stack.pc]) {

			case OpCodes.ADD_LITERAL:
				$_comment('ADD_LITERAL');
				type = $_list_get(row, 0);
				if (type == Types.NULL) {
					$_list_push(literalTable, VALUE_NULL);
				} else if (type == Types.BOOLEAN) {
					if (row[1] == 1) {
						$_list_push(literalTable, VALUE_TRUE);
					} else {
						$_list_push(literalTable, VALUE_FALSE);
					}
				} else if (type == Types.INTEGER) {
					$_list_push(literalTable, build_integer(row[1]));
				} else if (type == Types.FLOAT) {
					$_list_push(literalTable, new Value(Types.FLOAT, $_string_parse_float(stringArgs[StackFrame$stack.pc])));
				} else if (type == Types.STRING) {
					$_list_push(literalTable, new Value(Types.STRING, stringArgs[StackFrame$stack.pc]));
				}
				break;

			case OpCodes.ADD_NAME:
				$_comment('ADD_NAME');
				name = $_list_get(stringArgs, StackFrame$stack.pc);
				id = $_list_length(identifiers);
				$_dictionary_set(inv_identifiers, name, id);
				$_list_push(identifiers, name);
				if (name == 'length') {
					LENGTH_ID = id;
				}
				break;

			case OpCodes.ASSIGN_FUNCTION_ARG:
				$_comment('ASSIGN_FUNCTION_ARG');
				nameId = row[0];
				index = row[1];
				if (index >= $_list_length(StackFrame$stack.args)) {
					// pass in previous stack so that it's pointing at the spot where you invoke the function.
					return killExecution(StackFrame$stack.previous, "Not enough arguments supplied.");
				}
				value = $_list_get(StackFrame$stack.args, row[1]);
				$_dictionary_set(locals, nameId, value);
				break;

			case OpCodes.ASSIGN_FUNCTION_ARG_AND_JUMP:
				$_comment('ASSIGN_FUNCTION_ARG_AND_JUMP');
				nameId = row[0];
				index = row[1];
				if (index < $_list_length(StackFrame$stack.args)) {
					value = $_list_get(StackFrame$stack.args, index);
					$_dictionary_set(locals, nameId, value);
					StackFrame$stack.pc += row[2];
				}
				break;

			case OpCodes.ASSIGN_INDEX:
				$_comment('ASSIGN_INDEX');
				value = $_list_pop(valueStack);
				index = $_list_pop(valueStack);
				root = $_list_pop(valueStack);
				type = Value$root.type;
				if (type == Types.LIST) {
					if (Value$index.type == Types.INTEGER) {
						index = Value$index.internalValue;
						list = Value$root.internalValue;
						if (index < 0) {
							return killExecution(stack, "Index must be non-negative.");
						} else if (index >= $_list_length(list)) {
							return killExecution(stack, "Index is out of range.");
						} else {
							$_list_set(list, index, value);
						}
					} else {
						return killExecution(stack, "List index must be an integer.");
					}
				} else if (type == Types.DICTIONARY) {
					nativeKeyType = Value$index.type;
					if (nativeKeyType == Types.INTEGER) {
						nativeKey = Value$index.internalValue;
					} else if (nativeKeyType == Types.STRING) {
						nativeKey = Value$index.internalValue;
					} else if (nativeKeyType == Types.INSTANCE) {
						nativeKey = $_list_get(Value$index.internalValue, 2); // object ID
					} else {
						return killExecution(stack, "Invalid key for a dictionary.");
					}

					dictImpl = Value$root.internalValue;
					nonEmpty = DictImpl$dictImpl.size > 0;
					if (nonEmpty && DictImpl$dictImpl.keyType != nativeKeyType) {
						return killExecution(stack, "Cannot have multiple keys in one dictionary with different types.");
					}
					if (!$_dictionary_contains(DictImpl$dictImpl.keyLookup, nativeKey)) {
						$_dictionary_set(DictImpl$dictImpl.keyLookup, nativeKey, index);
						DictImpl$dictImpl.size += 1;
						if (!nonEmpty) {
							DictImpl$dictImpl.keyType = nativeKeyType;
						}
					}
					$_dictionary_set(DictImpl$dictImpl.valueLookup, nativeKey, value);
				} else {
					return killExecution(stack, getTypeFromId(type) + " type does not support indexing");
				}
				break;

			case OpCodes.ASSIGN_STEP:
				$_comment('ASSIGN_STEP');
				value = $_list_pop(valueStack);
				obj = $_list_pop(valueStack);
				nameId = row[0];
				if (Value$obj.type == Types.INSTANCE) {
					dict = $_list_get(Value$obj.internalValue, 1);
					$_dictionary_set(dict, nameId, value);
				} else {
					killExecution(stack, "Cannot assign to a field on this type.");
				}
				break;
				
			case OpCodes.ASSIGN_THIS_STEP:
				$_comment('ASSIGN_THIS_STEP');
				obj = StackFrame$stack.objectContext; // obj will never be null because that is a compile error.
				// TODO: prevent assignment to methods
				value = $_list_pop(valueStack);
				nameId = row[0];
				dict = $_list_get(Value$obj.internalValue, 1);
				$_dictionary_set(dict, nameId, value);
				break;

			case OpCodes.ASSIGN_VAR:
				$_comment('ASSIGN_VAR');
				thing = $_list_pop(valueStack);
				$_dictionary_set(locals, row[0], thing);
				break;

			case OpCodes.BINARY_OP:
				$_comment('BINARY_OP');
				rightObj = $_list_pop(valueStack);
				leftObj = $_list_pop(valueStack);

				leftType = Value$leftObj.type;
				if (rightObj == null) {
					return killExecution(stack, "WAT: " + valueToString(leftObj));
				}
				rightType = Value$rightObj.type;
				
				left = Value$leftObj.internalValue;
				right = Value$rightObj.internalValue;
				
				// TODO: special switch for when left == right
				op = $_list_get(row, 0);
				output = null;
				if (op == BinaryOps.EQUALS || op == BinaryOps.NOT_EQUALS) {
					if (leftType == rightType) {
						if (leftType <= Types.DICTIONARY) { // Note that this exists. Changing the ordering of types in the enum definition could ruin ==/!=
							if (left == right) { // may have to change if a platform doesn't support == for strings
								output = VALUE_TRUE;
							} else {
								output = VALUE_FALSE;
							}
						} else if (leftType == Types.INSTANCE) {
							if ($_list_get(left, 2) == $_list_get(right, 2)) {
								output = VALUE_TRUE;
							} else {
								output = VALUE_FALSE;
							}
						}
					} else {
						if ((leftType == Types.INTEGER && rightType == Types.FLOAT) || (leftType == Types.FLOAT && rightType == Types.INTEGER)) {
							if (left == right) {
								output = VALUE_TRUE;
							} else {
								output = VALUE_FALSE;
							}
						} else {
							output = VALUE_FALSE;
						}
					}
					
					if (output != null) {
						if (op == BinaryOps.NOT_EQUALS) {
							if (Value$output.internalValue) {
								output = VALUE_FALSE;
							} else {
								output = VALUE_TRUE;
							}
						}
						//$_list_push(valueStack, output);
					} else {
						return killExecution(stack, "== and != not defined here yet.");
					}
				} else if ((leftType == Types.STRING || rightType == Types.STRING) && op == BinaryOps.ADDITION && leftType != Types.NULL && rightType != Types.NULL) {
					output = new Value(Types.STRING, valueToString(leftObj) + valueToString(rightObj));
				} else {
					switch
						{((MAX_TYPE * MAX_OP + MAX_OP) * MAX_TYPE + MAX_TYPE)} // maximum possible value
						((((Value$leftObj.type * MAX_OP) + op) * MAX_TYPE) + Value$rightObj.type) {
						
						// int ** int
						case (Types.INTEGER * MAX_OP + BinaryOps.EXPONENT) * MAX_TYPE + Types.INTEGER:
							$_comment('int ** int');
							if (right == 0) output = INTEGERS_POS[1];
							else if (right > 0)
								output = build_integer($_int($_exponent(left, right)));
							else
								output = new Value(Types.FLOAT, $_exponent(left, right));
							break;

						// int ** float
						case (Types.INTEGER * MAX_OP + BinaryOps.EXPONENT) * MAX_TYPE + Types.FLOAT:
							$_comment('int ** float');
							output = new Value(Types.FLOAT, 0.0 + $_exponent(left, right));
							break;

						// float ** int
						case (Types.FLOAT * MAX_OP + BinaryOps.EXPONENT) * MAX_TYPE + Types.INTEGER:
							$_comment('float ** int');
							// add 0.0 just in case underlying platform likes to fall back to integer when possible
							output = new Value(Types.FLOAT, 0.0 + $_exponent(left, right));
							break;

						// float ** float
						case (Types.FLOAT * MAX_OP + BinaryOps.EXPONENT) * MAX_TYPE + Types.FLOAT:
							$_comment('float ** float');
							output = new Value(Types.FLOAT, 0.0 + $_exponent(left, right));
							break;
						
						// float % float
						case (Types.FLOAT * MAX_OP + BinaryOps.MODULO) * MAX_TYPE + Types.FLOAT:
							$_comment('float % float');
							if (right == 0) return killExecution(stack, "Modulo by 0");
							t = left % right;
							if (t < 0) t += right;
							output = new Value(Types.FLOAT, t);
							break;

						// int % int
						case (Types.INTEGER * MAX_OP + BinaryOps.MODULO) * MAX_TYPE + Types.INTEGER:
							$_comment('int % int');
							if (right == 0) return killExecution(stack, "Modulo by 0");
							t = left % right;
							if (t < 0) t += right;
							output = build_integer(t);
							break;

						// list + list
						case (Types.LIST * MAX_OP + BinaryOps.ADDITION) * MAX_TYPE + Types.LIST:
							$_comment('list + list');
							output = new Value(Types.LIST, $_list_concat(left, right)); break;

						// int + int
						case (Types.INTEGER * MAX_OP + BinaryOps.ADDITION) * MAX_TYPE + Types.INTEGER:
							$_comment('int + int');
							output = build_integer(left + right); break;

						// float + int
						case (Types.FLOAT * MAX_OP + BinaryOps.ADDITION) * MAX_TYPE + Types.INTEGER:
							$_comment('float + int');
							output = new Value(Types.FLOAT, left + right); break;

						// int + float
						case (Types.INTEGER * MAX_OP + BinaryOps.ADDITION) * MAX_TYPE + Types.FLOAT:
							$_comment('int + float');
							output = new Value(Types.FLOAT, left + right); break;

						// float + float
						case (Types.FLOAT * MAX_OP + BinaryOps.ADDITION) * MAX_TYPE + Types.FLOAT:
							$_comment('float + float');
							output = new Value(Types.FLOAT, left + right); break;
							
						// int - int
						case (Types.INTEGER * MAX_OP + BinaryOps.SUBTRACTION) * MAX_TYPE + Types.INTEGER:
							$_comment('int - int');
							output = build_integer(left - right); break;
							
						// int - float
						case (Types.INTEGER * MAX_OP + BinaryOps.SUBTRACTION) * MAX_TYPE + Types.FLOAT:
							$_comment('int - float');
							output = new Value(Types.FLOAT, left - right); break;
							
						// float - int
						case (Types.FLOAT * MAX_OP + BinaryOps.SUBTRACTION) * MAX_TYPE + Types.INTEGER:
							$_comment('float - int');
							output = new Value(Types.FLOAT, left - right); break;
							
						// float - float
						case (Types.FLOAT * MAX_OP + BinaryOps.SUBTRACTION) * MAX_TYPE + Types.FLOAT:
							$_comment('float - float');
							output = new Value(Types.FLOAT, left - right); break;

						// int * int
						case (Types.INTEGER * MAX_OP + BinaryOps.MULTIPLICATION) * MAX_TYPE + Types.INTEGER:
							$_comment('int * int');
							output = build_integer(left * right); break;

						// float * int
						case (Types.FLOAT * MAX_OP + BinaryOps.MULTIPLICATION) * MAX_TYPE + Types.INTEGER:
							$_comment('float * int');
							output = new Value(Types.FLOAT, left * right); break;

						// int * float
						case (Types.INTEGER * MAX_OP + BinaryOps.MULTIPLICATION) * MAX_TYPE + Types.FLOAT:
							$_comment('int * float');
							output = new Value(Types.FLOAT, left * right); break;

						// float * float
						case (Types.FLOAT * MAX_OP + BinaryOps.MULTIPLICATION) * MAX_TYPE + Types.FLOAT:
							$_comment('float * float');
							output = new Value(Types.FLOAT, left * right); break;
							
						// int / int
						case (Types.INTEGER * MAX_OP + BinaryOps.DIVISION) * MAX_TYPE + Types.INTEGER:
							$_comment('int / int');
							if (right == 0) return killExecution(stack, "Division by 0"); output = build_integer($_unsafe_integer_division(left, right)); break;
							
						// int / float
						case (Types.INTEGER * MAX_OP + BinaryOps.DIVISION) * MAX_TYPE + Types.FLOAT:
							$_comment('int / float');
							if (right == 0) return killExecution(stack, "Division by 0"); output = new Value(Types.FLOAT, $_unsafe_float_division(left, right)); break;

						// float / int
						case (Types.FLOAT * MAX_OP + BinaryOps.DIVISION) * MAX_TYPE + Types.INTEGER:
							$_comment('float / int');
							if (right == 0) return killExecution(stack, "Division by 0"); output = new Value(Types.FLOAT, $_unsafe_float_division(left, right)); break;

						// float / float
						case (Types.FLOAT * MAX_OP + BinaryOps.DIVISION) * MAX_TYPE + Types.FLOAT:
							$_comment('float / float');
							if (right == 0) return killExecution(stack, "Division by 0"); output = new Value(Types.FLOAT, $_unsafe_float_division(left, right)); break;

						// int < int
						case (Types.INTEGER * MAX_OP + BinaryOps.LESS_THAN) * MAX_TYPE + Types.INTEGER:
							$_comment('int < int');
							if (left < right) output = VALUE_TRUE; else output = VALUE_FALSE; break;

						// int <= int
						case (Types.INTEGER * MAX_OP + BinaryOps.LESS_THAN_OR_EQUAL) * MAX_TYPE + Types.INTEGER:
							$_comment('int <= int');
							if (left <= right) output = VALUE_TRUE; else output = VALUE_FALSE; break;
							
						// float < int
						case (Types.FLOAT * MAX_OP + BinaryOps.LESS_THAN) * MAX_TYPE + Types.INTEGER:
							$_comment('float < int');
							if (left < right) output = VALUE_TRUE; else output = VALUE_FALSE; break;

						// float <= int
						case (Types.FLOAT * MAX_OP + BinaryOps.LESS_THAN_OR_EQUAL) * MAX_TYPE + Types.INTEGER:
							$_comment('float <= int');
							if (left <= right) output = VALUE_TRUE; else output = VALUE_FALSE; break;
							
						// int < float
						case (Types.INTEGER * MAX_OP + BinaryOps.LESS_THAN) * MAX_TYPE + Types.FLOAT:
							$_comment('int < float');
							if (left < right) output = VALUE_TRUE; else output = VALUE_FALSE; break;

						// int <= float
						case (Types.INTEGER * MAX_OP + BinaryOps.LESS_THAN_OR_EQUAL) * MAX_TYPE + Types.FLOAT:
							$_comment('int <= float');
							if (left <= right) output = VALUE_TRUE; else output = VALUE_FALSE; break;

						// float < float
						case (Types.FLOAT * MAX_OP + BinaryOps.LESS_THAN) * MAX_TYPE + Types.FLOAT:
							$_comment('float < float');
							if (left < right) output = VALUE_TRUE; else output = VALUE_FALSE; break;

						// float <= float
						case (Types.FLOAT * MAX_OP + BinaryOps.LESS_THAN_OR_EQUAL) * MAX_TYPE + Types.FLOAT:
							$_comment('float <= float');
							if (left <= right) output = VALUE_TRUE; else output = VALUE_FALSE; break;

						// int >= int
						case (Types.INTEGER * MAX_OP + BinaryOps.GREATER_THAN_OR_EQUAL) * MAX_TYPE + Types.INTEGER:
							$_comment('int >= int');
							if (left >= right) output = VALUE_TRUE; else output = VALUE_FALSE; break;

						//int > int
						case (Types.INTEGER * MAX_OP + BinaryOps.GREATER_THAN) * MAX_TYPE + Types.INTEGER:
							$_comment('int > int');
							if (left > right) output = VALUE_TRUE; else output = VALUE_FALSE; break;

						// float >= int
						case (Types.FLOAT * MAX_OP + BinaryOps.GREATER_THAN_OR_EQUAL) * MAX_TYPE + Types.INTEGER:
							$_comment('float >= int');
							if (left >= right) output = VALUE_TRUE; else output = VALUE_FALSE; break;

						// float > int
						case (Types.FLOAT * MAX_OP + BinaryOps.GREATER_THAN) * MAX_TYPE + Types.INTEGER:
							$_comment('float > int');
							if (left >= right) output = VALUE_TRUE; else output = VALUE_FALSE; break;

						// int >= float
						case (Types.INTEGER * MAX_OP + BinaryOps.GREATER_THAN_OR_EQUAL) * MAX_TYPE + Types.FLOAT:
							$_comment('int >= float');
							if (left >= right) output = VALUE_TRUE; else output = VALUE_FALSE; break;

						// int > float
						case (Types.INTEGER * MAX_OP + BinaryOps.GREATER_THAN) * MAX_TYPE + Types.FLOAT:
							$_comment('int > float');
							if (left > right) output = VALUE_TRUE; else output = VALUE_FALSE; break;

						// float >= float
						case (Types.FLOAT * MAX_OP + BinaryOps.GREATER_THAN_OR_EQUAL) * MAX_TYPE + Types.FLOAT:
							$_comment('float >= float');
							if (left >= right) output = VALUE_TRUE; else output = VALUE_FALSE; break;

						// float > float
						case (Types.FLOAT * MAX_OP + BinaryOps.GREATER_THAN) * MAX_TYPE + Types.FLOAT:
							$_comment('float > float');
							if (left > right) output = VALUE_TRUE; else output = VALUE_FALSE; break;

						// list * int
						case (Types.LIST * MAX_OP + BinaryOps.MULTIPLICATION) * MAX_TYPE + Types.INTEGER:
							$_comment('list * int');
							t = [];
							while (right > 0) {
								$_list_push(t, left);
								right -= 1;
							}
							output = new Value(Types.LIST, t);
							break;
							
						// int * list
						case (Types.INTEGER * MAX_OP + BinaryOps.MULTIPLICATION) * MAX_TYPE + Types.LIST:
							$_comment('int * list');
							t = [];
							while (left > 0) {
								$_list_push(t, right);
								left -= 1;
							}
							output = new Value(Types.LIST, t);
							break;

						default:
							$_comment('unrecognized op');
							return killExecution(stack, "The '" + getBinaryOpFromId($_list_get(row, 0)) + "' operator is not supported for these types: " + getTypeFromId(Value$leftObj.type) + " and " + getTypeFromId(Value$rightObj.type));
							break;
					}
				}

				$_list_push(valueStack, output);
				break;

			case OpCodes.BOOLEAN_NOT:
				$_comment('BOOLEAN_NOT');
				value = $_list_pop(valueStack);
				if (Value$value.type != Types.BOOLEAN) return killExecution(stack, "Boolean expected.");
				if (Value$value.internalValue) {
					$_list_push(valueStack, VALUE_FALSE);
				} else {
					$_list_push(valueStack, VALUE_TRUE);
				}
				break;

			case OpCodes.BUILD_SWITCH_INT:
				$_comment('BUILD_SWITCH_INT');
				integerSwitchLookup = {};
				for (i = 0; i < $_list_length(row); i += 2) {
					$_dictionary_set(integerSwitchLookup, $_list_get(row, i), $_list_get(row, i + 1));
				}
				p = $_get_program_data();
				$_list_push(ProgramData$p.integerSwitchLookups, integerSwitchLookup);
				break;

			case OpCodes.BUILD_SWITCH_STRING:
				$_comment('BUILD_SWITCH_STRING');
				allLookups = ProgramData$p.stringSwitchLookups;
				id = $_list_get(row, 0);
				while ($_list_length(allLookups) <= id) {
					$_list_push(allLookups, {});
				}
				lookup = $_list_get(allLookups, id);
				key = $_list_get(stringArgs, StackFrame$stack.pc);
				$_dictionary_set(lookup, key, $_list_get(row, 1));
				break;

			case OpCodes.CALL_BASE_CONSTRUCTOR:
				$_comment('CALL_BASE_CONSTRUCTOR');
				sc_row = $_list_get(specialCache, StackFrame$stack.pc);
				classId = $_list_get(sc_row, 0);
				classData = $_list_get(classTable, classId);
				value = StackFrame$stack.objectContext;
				
				// This is copied identically below in CALL_CONSTRUCTOR
				pc = $_list_get(classData, 1);
				argCount = $_list_get(row, 0);
				if (pc != 0) {
					funcArgs = [];
					while (argCount > 0) {
						$_list_push(funcArgs, $_list_pop(valueStack));
						argCount -= 1;
					}
					if ($_list_get(row, 0) > 1) {
						$_list_reverse_in_place(funcArgs);
					}

					valueStack = [];
					locals = {};
					stack = new StackFrame(pc - 1, locals, valueStack, funcArgs, stack, false, value);
				} else {
					$_list_push(valueStack, value);
				}
				break;

			case OpCodes.CALL_CONSTRUCTOR:
				$_comment('CALL_CONSTRUCTOR');
				sc_row = $_list_get(specialCache, StackFrame$stack.pc);
				classId = $_list_get(sc_row, 0);
				ProgramData$p.instanceCounter += 1;
				value = new Value(Types.INSTANCE, [classId, {}, ProgramData$p.instanceCounter]);
				classData = $_list_get(classTable, classId);
				if (classData[5] == 0) {
					return killExecution(stack, "This class has not been defined yet.");
				}
				pc = $_list_get(classData, 1);
				argCount = $_list_get(row, 0);
				if (pc != 0) {
					funcArgs = [];
					returnValueUsed = $_list_get(row, 2) == 1;
					while (argCount > 0) {
						$_list_push(funcArgs, $_list_pop(valueStack));
						argCount -= 1;
					}
					if ($_list_get(row, 0) > 1) {
						$_list_reverse_in_place(funcArgs);
					}

					if (returnValueUsed) {
						$_list_push(valueStack, value);
					}

					valueStack = [];
					locals = {};
					stack = new StackFrame(pc - 1, locals, valueStack, funcArgs, stack, false, value);
				} else {
					$_list_push(valueStack, value);
				}
				break;

			case OpCodes.CALL_FRAMEWORK_FUNCTION:
				$_comment('CALL_FRAMEWORK_FUNCTION');
				// TODO: the types for these values must be verified.
				// The arg count is verified to be in the correct range at compile time.
				// However if there are optional arguments, the number of args used in this call are located in row[2]
				output = VALUE_NULL;
				switch (row[0]) {
					case %%%FF_ABS%%%:
						value = $_list_pop(valueStack);
						if (Value$value.type == Types.INTEGER) {
							if (Value$value.internalValue < 0) {
								output = build_integer(-Value$value.internalValue);
							} else {
								output = value;
							}
						} else if (Value$value.type == Types.FLOAT) {
							if (Value$value.internalValue < 0) {
								output = new Value(Types.FLOAT, -Value$value.internalValue);
							} else {
								output = value;
							}
						} else {
							return killExecution(stack, "$abs requires a number as input.");
						}
						break;

					case %%%FF_ARCTAN2%%%:
						x = $_list_pop(valueStack);
						y = $_list_pop(valueStack);
						$_insert_platform_code('ff_arctan2');
						break;

					case %%%FF_ASSERT%%%:
						msg = $_list_pop(valueStack);
						value = $_list_pop(valueStack);
						if (Value$value.type != Types.BOOLEAN || !Value$value.internalValue) {
							return killExecution(stack, "Assertion failed: " + valueToString(msg));
						}
						break;

					case %%%FF_BLIT_IMAGE%%%:
						y = $_list_pop(valueStack);
						x = $_list_pop(valueStack);
						image = $_list_pop(valueStack);
						$_insert_platform_code('ff_blit_image');
						break;
						
					case %%%FF_CLOCK_TICK%%%:
						ProgramData$p.stackTop = stack;
						StackFrame$stack.pc += 1;
						return null;
						// this extra break is not a mistake. Python translation will get confused without the break here. Ultimately I plan to make this smart enough that it'll figure it out, but it's not a high priority.
						break;

					case %%%FF_COS%%%:
						x = $_list_pop(valueStack);
						$_insert_platform_code('ff_cos');
						break;

					case %%%FF_CURRENT_TIME%%%:
						$_insert_platform_code('ff_current_time');
						break;
						
					case %%%FF_DOWNLOAD_IMAGE%%%:
						url = $_list_pop(valueStack);
						key = $_list_pop(valueStack);
						$_insert_platform_code('ff_download_image');
						break;

					case %%%FF_DRAW_ELLIPSE%%%:
						alpha = $_list_pop(valueStack);
						blue = $_list_pop(valueStack);
						green = $_list_pop(valueStack);
						red = $_list_pop(valueStack);
						height = $_list_pop(valueStack);
						width = $_list_pop(valueStack);
						top = $_list_pop(valueStack);
						left = $_list_pop(valueStack);
						$_insert_platform_code('ff_draw_ellipse');
						break;

					case %%%FF_DRAW_LINE%%%:
						alpha = $_list_pop(valueStack);
						blue = $_list_pop(valueStack);
						green = $_list_pop(valueStack);
						red = $_list_pop(valueStack);
						width = $_list_pop(valueStack);
						y2 = $_list_pop(valueStack);
						x2 = $_list_pop(valueStack);
						y1 = $_list_pop(valueStack);
						x1 = $_list_pop(valueStack);
						$_insert_platform_code('ff_draw_line');
						break;

					case %%%FF_DRAW_RECTANGLE%%%:
						alpha = $_list_pop(valueStack);
						blue = $_list_pop(valueStack);
						green = $_list_pop(valueStack);
						red = $_list_pop(valueStack);
						height = $_list_pop(valueStack);
						width = $_list_pop(valueStack);
						y = $_list_pop(valueStack);
						x = $_list_pop(valueStack);
						$_insert_platform_code('ff_draw_rectangle');
						break;

					case %%%FF_FILL_SCREEN%%%:
						blue = $_list_pop(valueStack);
						green = $_list_pop(valueStack);
						red = $_list_pop(valueStack);
						$_insert_platform_code('ff_fill_screen');
						break;
						
					case %%%FF_FLIP_IMAGE%%%:
						y = $_list_pop(valueStack);
						x = $_list_pop(valueStack);
						img = $_list_pop(valueStack);
						$_insert_platform_code('ff_flip_image');
						break;

					case %%%FF_FLOOR%%%:
						value = $_list_pop(valueStack);
						$_insert_platform_code('ff_floor');
						break;

					case %%%FF_GET_EVENTS%%%:
						$_insert_platform_code('ff_get_events');
						break;
						
					case %%%FF_GET_IMAGE%%%:
						key = $_list_pop(valueStack);
						$_insert_platform_code('ff_get_image');
						break;
						
					case %%%FF_GET_IMAGE_HEIGHT%%%:
						value = $_list_pop(valueStack);
						$_insert_platform_code('ff_get_image_height');
						break;

					case %%%FF_GET_IMAGE_WIDTH%%%:
						value = $_list_pop(valueStack);
						$_insert_platform_code('ff_get_image_width');
						break;

					case %%%FF_INITIALIZE_GAME%%%: 
						fps = $_list_pop(valueStack);
						$_insert_platform_code('ff_initialize_game');
						break;
						
					case %%%FF_INITIALIZE_SCREEN%%%:
						height = $_list_pop(valueStack);
						width = $_list_pop(valueStack);
						$_insert_platform_code('ff_initialize_screen');
						break;
						
					case %%%FF_INITIALIZE_SCREEN_SCALED%%%:
						pheight = $_list_pop(valueStack);
						pwidth = $_list_pop(valueStack);
						height = $_list_pop(valueStack);
						width = $_list_pop(valueStack);
						$_insert_platform_code('ff_initialize_screen_scaled');
						break;
						
					case %%%FF_IS_IMAGE_LOADED%%%:
						key = $_list_pop(valueStack);
						$_insert_platform_code('ff_is_image_loaded');
						break;

					case %%%FF_PARSE_INT%%%:
						value = $_list_pop(valueStack);
						$_insert_platform_code('ff_parse_int');
						break;

					case %%%FF_PRINT%%%: 
						value = $_list_pop(valueStack);
						string1 = valueToString(value);
						$_insert_platform_code('ff_print'); 
						break;
					case %%%FF_RANDOM%%%:
						$_insert_platform_code('ff_random');
						break;
					case %%%FF_SET_TITLE%%%:
						value = $_list_pop(valueStack);
						value = valueToString(value);
						$_insert_platform_code('ff_set_title');
						break;

					case %%%FF_SIN%%%:
						x = $_list_pop(valueStack);
						$_insert_platform_code('ff_sin');
						break;


					case %%%FF_TYPEOF%%%:
						value = $_list_pop(valueStack);

						switch (Value$value.type) {
							case Types.NULL: t = "null"; break;
							case Types.INTEGER: t = "integer"; break;
							case Types.FLOAT: t = "float"; break;
							case Types.STRING: t = "string"; break;
							case Types.LIST: t = "list"; break;
							case Types.DICTIONARY: t = "dictionary"; break;
							case Types.INSTANCE: t = "instance"; break;
							case Types.FUNCTION: t = "function"; break;
							case Types.METHOD: t = "method"; break;
							case Types.SYS_METHOD: t = "primitive_method"; break;
							case Types.NATIVE_OBJECT: t = "native_system_object"; break;
							default: t = "unknown_type"; break;
						}
						output = new Value(Types.STRING, t);
						break;
					default: 
						return killExecution(stack, "Invalid framework function: " + $_string_cast_weak(row[0])); 
						break;
				}
				if (row[1] == 1) {
					$_list_push(valueStack, output);
				}
				break;

			case OpCodes.CALL_FUNCTION:
				$_comment('CALL_FUNCTION');
				// This is mostly replicated below. Be sure to keep changes in sync.
				func = $_list_pop(valueStack);
				funcArgs = [];
				argCount = $_list_get(row, 0);
				returnValueUsed = $_list_get(row, 1) == 1;
				while (argCount > 0) {
					$_list_push(funcArgs, $_list_pop(valueStack));
					argCount -= 1;
				}
				if ($_list_get(row, 0) > 1) {
					$_list_reverse_in_place(funcArgs);
				}
				funcType = Value$func.type;
				if (funcType == Types.SYS_METHOD) {
					sysMethod = Value$func.internalValue;
					context = $_list_get(sysMethod, 0);
					methodId = $_list_get(sysMethod, 1);
					error = callSystemMethod(methodId, context, funcArgs, valueStack, returnValueUsed);
					if (error != null) {
						return killExecution(stack, error);
					}
				} else if (funcType == Types.METHOD) {
					methodData = Value$func.internalValue;
					obj = methodData[0];
					pc = methodData[1];
					maxArgs = methodData[2];
					if (maxArgs < $_list_length(funcArgs)) {
						return killExecution(stack, "Too many args were passed in.");
					}
					valueStack = [];
					locals = {};
					stack = new StackFrame(pc - 1, locals, valueStack, funcArgs, stack, returnValueUsed, obj);
				} else if (funcType == Types.FUNCTION) {
					valueStack = [];
					locals = {};
					stack = new StackFrame(Value$func.internalValue[0], locals, valueStack, funcArgs, stack, returnValueUsed, null);
				} else {
					return killExecution(stack, "Cannot invoke " + getTypeFromId(funcType) + " like a function.");
				}
				break;

			case OpCodes.CALL_FUNCTION_ON_VARIABLE:
				$_comment('CALL_FUNCTION_ON_VARIABLE');
				// This is mostly replicated above. Be sure to keep changes in sync.
				funcArgs = [];
				argCount = row[1];
				returnValueUsed = $_list_get(row, 2) == 1;
				while (argCount > 0) {
					$_list_push(funcArgs, $_list_pop(valueStack));
					argCount -= 1;
				}
				if ($_list_get(row, 1) > 1) {
					$_list_reverse_in_place(funcArgs);
				}
				funcPointer = $_dictionary_get(locals, row[0], null);
				if (funcPointer == null) {
					funcPointer = $_dictionary_get(globals, row[0], null);
				}

				if (funcPointer != null) {
					
					type = Value$funcPointer.type;
					if (type == Types.FUNCTION) {
						valueStack = [];
						locals = {};
						stack = new StackFrame(Value$funcPointer.internalValue[0], locals, valueStack, funcArgs, stack, returnValueUsed, null);
					} else {
						return killExecution(stack, "Cannot invoke " + getTypeFromId(type) + " types like a function.");
					}
				} else {
					return killExecution(stack, "That function is not defined: '" + identifiers[row[0]] + "'");
				}
				break;

			case OpCodes.CLASS_DEFINITION:
				$_comment('CLASS_DEFINITION');
				sc_row = $_list_get(specialCache, StackFrame$stack.pc);
				classId = $_list_get(sc_row, 0);
				classInfo = $_list_get(classTable, classId);
				classInfo[5] = 1;
				break;

			case OpCodes.DEF_DICTIONARY:
				$_comment('DEF_DICT');
				dictKeyLookup = {};
				dictValueLookup = {};
				size = row[0];
				keyType = Types.INTEGER; // always ignored when size is 0
				prevType = null;
				first = true;
				anticipatedSize = size;
				while (size > 0) {
					value = $_list_pop(valueStack);
					key = $_list_pop(valueStack);
					keyType = Value$key.type;
					if (first) prevType = keyType;
					if (!first && keyType != prevType) return killExecution(stack, "Cannot have multiple key types in one dictionary.");

					if (keyType == Types.INTEGER || keyType == Types.STRING) {
						nativeKey = Value$key.internalValue;
					} else if (keyType == Types.INSTANCE) {
						obj = Value$key.internalValue;
						objId = $_list_get(obj, 2);
						nativeKey = objId;
					} else {
						return killExecution(stack, "Only integers, strings, and objects can be used as dictionary keys.");
					}

					$_dictionary_set(dictKeyLookup, nativeKey, key);
					$_dictionary_set(dictValueLookup, nativeKey, value);

					first = false;
					size -= 1;
				}

				if ($_dictionary_size(dictKeyLookup) != anticipatedSize) {
					return killExecution(stack, "Key collision"); // TODO: do a better job of figuring out where this collision is.
				}

				dict = new Value(Types.DICTIONARY, new DictImpl(dictKeyLookup, dictValueLookup, anticipatedSize, keyType));
				$_list_push(valueStack, dict);
				break;

			case OpCodes.DEF_LIST:
				$_comment('DEF_LIST');
				value = [];
				size = row[0];
				while (size > 0) {
					$_list_push(value, $_list_pop(valueStack));
					size -= 1;
				}
				$_list_reverse_in_place(value);
				$_list_push(valueStack, new Value(Types.LIST, value));
				break;

			case OpCodes.DEF_ORIGINAL_CODE:
				fileId = $_list_get(row, 0);
				content = $_list_get(ProgramData$p.stringArgs, StackFrame$stack.pc);
				codeLookup = ProgramData$p.sourceCode;
				while ($_list_length(codeLookup) <= fileId) {
					$_list_push(codeLookup, null);
				}
				$_list_set(codeLookup, fileId, content);
				break;

			case OpCodes.DEREF_DOT:
				$_comment('DEREF_DOT');
				// TODO: no caching of methods occur here because ideally, I want to create another op for dotStepInvoke which eliminates the need to instantiate the method in 99.5% of cases.
				root = $_list_pop(valueStack);
				type = Value$root.type;
				if (type == Types.INSTANCE) {
					dict = $_list_get(Value$root.internalValue, 1);
					value = $_dictionary_get(dict, row[0], null);
					if (value == null) {
						classId = $_list_get(Value$root.internalValue, 0);
						method = $_dictionary_get($_list_get(classMethods, classId), row[0], null);
						if (method == null) {
							return killExecution(stack, "Field used before it was defined.");
						} else {
							value = new Value(Types.METHOD, [root, method[2], method[1]]); // context, pc, maxArgs
							$_dictionary_set(dict, row[0], value);
							$_list_push(valueStack, value);
						}
					} else {
						$_list_push(valueStack, value);
					}
				} else if (row[0] == LENGTH_ID) { // Length is a field. All other primitive type stuff is not.
					len = -1;
					if (type == Types.LIST) {
						// might need to revisit this if other platforms aren't as easy to do a list implementation?
						len = $_list_length(Value$root.internalValue);
					} else if (type == Types.DICTIONARY) {
						t = Value$root.internalValue;
						len = DictImpl$t.size;
					} else if (type == Types.STRING) {
						len = $_string_length(Value$root.internalValue);
					} else {
						return killExecution(stack, getTypeFromId(type) + " does not have a length property.");
					}

					if (len < INTEGER_POSITIVE_CACHE) {
						$_list_push(valueStack, INTEGERS_POS[len]);
					} else {
						$_list_push(valueStack, new Value(Types.INTEGER, len));
					}
				} else {
					// Everything else will presumably be hit 99% of the time via the DOT_STEP_INVOKE op instead of here, since they're methods. 
					// Therefore I'm not particularly concerned about optimizing the rest of this.

					primitive_method_id = $_dictionary_get(primitiveMethodLookup, $_string_cast_weak(type) + identifiers[row[0]], -1);
					if (primitive_method_id == -1) {
						return killExecution(stack, getTypeFromId(type) + " type does not have a field called '" + identifiers[row[0]] + "'.");
					} else {
						$_list_push(valueStack, new Value(Types.SYS_METHOD, [root, primitive_method_id]));
					}
				}
				break;

			case OpCodes.DUPLICATE_STACK_TOP:
				$_comment('DUPLICATE_STACK_TOP');
				if (row[0] == 1) {
					$_list_push(valueStack, $_list_get(valueStack, $_list_length(valueStack) - 1));
				} else {
					for (i = 0; i < row[0]; i += 1) {
						$_list_push(valueStack, $_list_get(valueStack, $_list_length(valueStack) - row[0]));
					}
				}
				break;

			case OpCodes.FUNCTION_DEFINITION:
				$_comment('FUNCTION_DEFINITION');
				if (row[3] == 1) {
					
				} else {
					funcDef = new Value(Types.FUNCTION, 
						// PC                             Max arg count    Name ID]
						[StackFrame$stack.pc + row[1],    row[2],          identifiers[row[0]]]);
					$_list_push(valueStack, funcDef);
				}
				break;

			case OpCodes.INDEX:
				$_comment('INDEX');
				index = $_list_pop(valueStack);
				root = $_list_pop(valueStack);
				if (Value$root.type == Types.LIST) {
					if (Value$index.type != Types.INTEGER) {
						return killExecution(stack, "List index must be an integer.");
					} else {
						indexValue = Value$index.internalValue;
						listValue = Value$root.internalValue;
						if (indexValue < 0) return killExecution(stack, "List index cannot be negative.");
						else if (indexValue >= $_list_length(listValue)) return killExecution(stack, "List index is out of bounds");
						else {
							item = $_list_get(listValue, indexValue);
							$_list_push(valueStack, item);
						}
					}
				} else if (Value$root.type == Types.DICTIONARY) {
					dictImpl = Value$root.internalValue;
					nativeKeyType = Value$index.type;
					if (nativeKeyType != DictImpl$dictImpl.keyType) {
						return killExecution(stack, "Key not found.");
					} else {
						if (nativeKeyType == Types.INTEGER) {
							nativeKey = Value$index.internalValue;
						} else if (nativeKeyType == Types.STRING) {
							nativeKey = Value$index.internalValue;
						} else if (nativeKeyType == Types.INSTANCE) {
							nativeKey = $_list_get(Value$index.internalValue, 2);
						} else {
							return killExecution(stack, "Key not found.");
						}

						value = $_dictionary_get(DictImpl$dictImpl.valueLookup, nativeKey, null);
						if (value == null) return killExecution(stack, "Key not found.");
							
						$_list_push(valueStack, value);
					}
				} else if (Value$root.type == Types.STRING) {
					t = Value$root.internalValue;
					if (Value$index.type != Types.INTEGER) {
						return killExecution(stack, "indexes into strings must be integers.");
					}
					index = Value$index.internalValue;
					if (index < 0 || index >= $_string_length(t)) {
						return killExecution(stack, "String index out of range.");
					}
					$_list_push(valueStack, new Value(Types.STRING, $_string_char_at(t, index)));
				} else {
					return killExecution(stack, "Cannot index into this type: " + $_string_cast_weak(Value$root.type) + ", " + $_list_get(identifiers, args[StackFrame$stack.pc - 2][0]));
				}
				break;

			case OpCodes.INDEX_INT:
				$_comment('INDEX_INT');
				return killExecution(stack, "TODO: index int");
				break;

			case OpCodes.INDEX_STRING:
				$_comment('INDEX_STRING');
				return killExecution(stack, "TODO: index string");
				break;

			case OpCodes.ITERATION_STEP:
				$_comment('ITERATION_STEP');
				stacklen = $_list_length(valueStack);
				index = $_list_get(valueStack, stacklen - 3);
				varId = $_list_get(valueStack, stacklen - 2);
				list = $_list_get(valueStack, stacklen - 1);
				index = Value$index.internalValue;
				list = Value$list.internalValue;
				varId = Value$varId.internalValue;
				listlen = $_list_length(list);
				if (index < listlen) {
					$_dictionary_set(locals, varId, $_list_get(list, index));
				} else {
					StackFrame$stack.pc += row[0];
				}
				index += 1;
				$_list_set(valueStack, stacklen - 3, build_integer(index));
				break;

			case OpCodes.JUMP:
				$_comment('JUMP');
				StackFrame$stack.pc += row[0];
				break;
			
			case OpCodes.JUMP_IF_FALSE:
				$_comment('JUMP_IF_FALSE');
				expr = $_list_pop(valueStack);
				if (Value$expr.type != Types.BOOLEAN) {
					return killExecution(stack, "Boolean expected.");
				} else if (!Value$expr.internalValue) {
					StackFrame$stack.pc += row[0];
				}
				break;

			case OpCodes.JUMP_IF_FALSE_NO_POP:
				$_comment('JUMP_IF_FALSE_NON_POP');
				expr = $_list_pop(valueStack);
				if (Value$expr.type != Types.BOOLEAN) {
					return killExecution(stack, "Boolean expected.");
				} else if (!Value$expr.internalValue) {
					StackFrame$stack.pc += row[0];
					$_list_push(valueStack, VALUE_FALSE);
				}
				break;

			case OpCodes.JUMP_IF_TRUE:
				$_comment('JUMP_IF_TRUE');
				expr = $_list_pop(valueStack);
				if (Value$expr.type != Types.BOOLEAN) {
					return killExecution(stack, "Boolean expected.");
				} else if (Value$expr.internalValue) {
					StackFrame$stack.pc += row[0];
				}
				break;

			case OpCodes.JUMP_IF_TRUE_NO_POP:
				$_comment('JUMP_IF_TRUE_NO_POP');
				expr = $_list_pop(valueStack);
				if (Value$expr.type != Types.BOOLEAN) {
					return killExecution(stack, "Boolean expected.");
				} else if (Value$expr.internalValue) {
					StackFrame$stack.pc += row[0];
					$_list_push(valueStack, VALUE_TRUE);
				}
				break;
				
			case OpCodes.LITERAL:
				$_comment('LITERAL');
				$_list_push(valueStack, $_list_get(literalTable, $_list_get(row, 0)));
				break;

			case OpCodes.LITERAL_STREAM:
				$_comment('LITERAL_STREAM');
				for (i = $_list_length(row) - 1; i >= 0; i -= 1) {
					$_list_push(valueStack, $_list_get(literalTable, $_list_get(row, i)));
				}
				break;

			case OpCodes.NEGATIVE_SIGN:
				$_comment('NEGATIVE_SIGN');
				value = $_list_pop(valueStack);
				t = Value$value.type;
				value = Value$value.internalValue;
				if (t == Types.INTEGER) {
					value = build_integer(-value);
				} else if (t == Types.FLOAT) {
					value = new Value(Types.FLOAT, -value);
				} else {
					return killExecution(stack, "Negative sign can only be applied to numbers. Found " + typeToString(t) + " instead.");
				}

				$_list_push(valueStack, value);
				break;

			case OpCodes.POP:
				$_comment('POP');
				$_list_pop(valueStack);
				break;

			case OpCodes.RETURN:
				$_comment('RETURN');
				returnValue = $_list_pop(valueStack);
				useReturnValue = StackFrame$stack.returnValueUsed;
				stack = StackFrame$stack.previous;
				if (stack == null) {
					StackFrame$stack.pc += 1;
					return '';
				}
				valueStack = StackFrame$stack.valueStack;
				locals = StackFrame$stack.locals;
				if (useReturnValue) {
					$_list_push(valueStack, returnValue);
				}
				break;
			
			case OpCodes.RETURN_NULL:
				$_comment('RETURN_NULL');
				useReturnValue = StackFrame$stack.returnValueUsed;
				stack = StackFrame$stack.previous;
				if (stack == null) {
					return '';
				}
				locals = StackFrame$stack.locals;
				valueStack = StackFrame$stack.valueStack;
				if (useReturnValue) {
					$_list_push(valueStack, VALUE_NULL);
				}
				break;

			case OpCodes.SWITCH_INT:
				$_comment('SWITCH_INT');
				value = $_list_pop(valueStack);
				if (Value$value.type == Types.INTEGER) {
					key = Value$value.internalValue;
					offset = $_dictionary_get($_list_get(integerSwitches, $_list_get(row, 0)), key, $_list_get(row, 1));
					StackFrame$stack.pc += offset;
				} else {
					return killExecution(stack, "Switch statement expects an integer.");
				}
				break;

			case OpCodes.SWITCH_STRING:
				$_comment('SWITCH_STRING');
				value = $_list_pop(valueStack);
				if (Value$value.type == Types.STRING) {
					key = Value$value.internalValue;
					offset = $_dictionary_get($_list_get(stringSwitches, $_list_get(row, 0)), key, $_list_get(row, 1));
					StackFrame$stack.pc += offset;
				} else {
					return killExecution(stack, "Switch statement expects a string.");
				}
				break;

			case OpCodes.THIS:
				$_comment('THIS');
				// will never be called outside of a method.
				$_list_push(valueStack, StackFrame$stack.objectContext);
				break;

			case OpCodes.TOKEN_DATA:
				$_comment('TOKEN_DATA');
				tokensByPc = ProgramData$p.tokenData;
				pc = $_list_get(row, 0) + ProgramData$p.userCodeStart;
				line = $_list_get(row, 1);
				col = $_list_get(row, 2);
				file = $_list_get(row, 3);
				tokens = $_list_get(tokensByPc, pc);
				if (tokens == null) {
					tokens = [];
					$_list_set(tokensByPc, pc, tokens);
				}
				$_list_push(tokens, new Token(line, col, file));
				break;

			case OpCodes.USER_CODE_START:
				$_comment('USER_CODE_START');
				value = $_list_get(row, 0);
				ProgramData$p.userCodeStart = value;
				break;

			case OpCodes.VARIABLE:
				$_comment('VARIABLE');
				value = $_dictionary_get(locals, row[0], null);
				if (value == null) {
					value = $_dictionary_get(globals, row[0], null);
					if (value == null) {
						return killExecution(stack, "Variable not defined: " + identifiers[row[0]]);
					} else {
						$_list_push(valueStack, value);
					}
				} else {
					$_list_push(valueStack, value);
				}
				break;

			case OpCodes.VARIABLE_STREAM:
				$_comment('VARIABLE_STREAM');
				// TODO: trim off the token data from the args now that this is done differently.
				for (i = 0; $_list_get(row, i) != -1; i += 1) {
					value = $_dictionary_get(locals, $_list_get(row, i), null);
					if (value == null) {
						value = $_dictionary_get(globals, $_list_get(row, i), null);
					}
					if (value == null) {
						sc_row = $_list_get(specialCache, StackFrame$stack.pc);
						fakeToken = $_list_get(sc_row, i);
						return killExecutionWithTokenOverride(stack, fakeToken, "Variable not defined: " + identifiers[$_list_get(row, i)]);
					} else {
						$_list_push(valueStack, value);
					}
				}
				break;

			case OpCodes.VERIFY_IS_LIST:
				$_comment('VERIFY_IS_LIST');
				value = $_list_pop(valueStack);
				if (Value$value.type != Types.LIST) {
					return killExecution(stack, "Expected a list.");
				}
				$_list_push(valueStack, value);
				break;

			default:
				$_comment('THIS SHOULD NEVER HAPPEN');
				return killExecution(stack, "Bad op code: " + $_string_cast_weak(ops[StackFrame$stack.pc]));
		}
		StackFrame$stack.pc += 1;
	}
}

function killExecution(stack, message) {
	return killExecutionWithTokenOverride(stack, null, message);
}

function killExecutionWithTokenOverride(stack, firstTokenOverride, message) {
	fullMessage = '';
	programData = $_get_program_data(); 
	allTokens = ProgramData$programData.tokenData;
	allFiles = generateSourceFiles();
	first = true;
	while (stack != null) {
		pc = StackFrame$stack.pc;
		stack = StackFrame$stack.previous;

		token = null;

		tokenlist = $_list_get(allTokens, pc);
		if (tokenlist != null) {
			token = $_list_get(tokenlist, 0);
		}
		if (first && firstTokenOverride != null) {
			token = firstTokenOverride;
		}

		if (token != null) {
			line = $_list_get(token, 0);
			col = $_list_get(token, 1);
			fileId = $_list_get(token, 2);
			if (fileId < $_list_length(allFiles)) {
				fileData = $_list_get(allFiles, fileId);
				lines = $_string_split(fileData, '\n');
				filename = lines[0];
				linevalue = lines[line + 1];
				fullMessage = filename + ", Line: " + $_string_cast_weak(line + 1) + ", Col: " + $_string_cast_weak(col + 1) + "\n" + fullMessage;
				if (first) {
					linedisplay = '';
					arrowdisplay = '';
					lefttrim = true;
					for (i = 0; i < $_string_length(linevalue); i += 1) {
						c = $_string_char_at(linevalue, i);
						if (lefttrim) {
							if (c == ' ' || c == '\t' || c == '\r') {
								col -= 1;
							} else {
								lefttrim = false;
								i -= 1;
							}
						} else {
							if (col == 0) {
								arrowdisplay += '^';
								linedisplay += c;
							} else if (c == '\t') {
								arrowdisplay += '    ';
								linedisplay += '    ';
							} else {
								arrowdisplay += ' ';
								linedisplay += c;
							}
							col -= 1;
						}
					}
					lineCopy = linevalue;

					fullMessage = fullMessage + "\n  " + message + "\n\n" + linedisplay + "\n" + arrowdisplay;
				}
			} else {
				token = null; // execution killed before file data was loaded
			}
		}

		if (token == null) {
			fullMessage = "[No Stack Info]\n" + fullMessage;
			if (first) {
				fullMessage += "[No Stack Info]\n  " + message + "\n";
			}
		}

		first = false;
		token = null;
	}
	return "Error:\n" + fullMessage;
}
