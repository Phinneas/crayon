import Math;

namespace Nori {

	/*
		nativeData[0] -> root UI element
		nativeData[1] -> platform window object
	*/

	class Window {
		field content = null;
		
		field bounds = [0, 0, 600, 400];
		field result = null;
		field _isShown = false;
		field _closeTimeResumeToken = null;
		
		constructor() { }
		
		function close() {
			$$lib_nori_closeWindowAndMaybeResumeExecution(this, this._closeTimeResumeToken);
		}
		
		function show() { return this._showImpl(true); }
		function showNonBlocking() { return this._showImpl(false); }
		
		function _showImpl(blocking) {
			if (this._isShown)
				Core.assert(false, "Window is already shown.");
			this._isShown = true;
			
			token = Core.getCurrentExecutionToken();
			instantResumeToken = null;
			this._closeTimeResumeToken = null;
			if (blocking) {
				this._closeTimeResumeToken = token;
			} else {
				instantResumeToken = token;
			}
			
			$$lib_nori_openWindowAndSuspendExecution(
				this,
				Math.floor(this.bounds[2]),
				Math.floor(this.bounds[3]),
				this._internalRender,
				this._invokeMouseEvent,
				this._closeTimeResumeToken,
				instantResumeToken);
			
			return blocking ? this.result : null;
		}
			
		function _invokeMouseEvent(sender, handlers, button, x, y) {
			for (handler : handlers) {
				handler(sender, new MouseEventArgs(button, x, y));
			}
		}
		
		function setSize(width, height) {
			this.bounds[2] = Math.floor(width);
			this.bounds[3] = Math.floor(height);
			return this;
		}
		
		function setContent(element) {
			this.content = element;
			return this;
		}
		
		field boundsOut = [null] * 4;
		
		function _internalRender() {
			if (this.content != null) {
				this.content._internalRender(this, this.bounds, this.boundsOut);
			}
		}
	}
}
