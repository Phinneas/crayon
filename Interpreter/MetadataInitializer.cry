function @type('int') initializeFunction(@type('ProgramData') p, @type('Array<int>') args, @type('int') currentPc) {

	@type('int') functionId = $_array_get(args, 0);
	@type('int') nameId = $_array_get(args, 1);
	@type('int') minArgCount = $_array_get(args, 2);
	@type('int') maxArgCount = $_array_get(args, 3);
	@type('int') functionType = $_array_get(args, 4);
	@type('int') classId = $_array_get(args, 5);
	@type('int') localsCount = $_array_get(args, 6);
	@type('int') pcJump = $_array_get(args, 7);
	@type('int') numPcOffsetsForOptionalArgs = $_array_get(args, 8);

	@type('Array<int>') pcOffsetsForOptionalArgs = $_new_array('int', numPcOffsetsForOptionalArgs + 1);
	for (@type('int') i = 0; i < numPcOffsetsForOptionalArgs; i += 1) {
		$_array_set(pcOffsetsForOptionalArgs, i + 1, $_array_get(args, 9 + i));
	}

	@type('Array<FunctionInfo>') functionTable = getFunctionTable(p, functionId);
	$_array_set(functionTable, functionId, new FunctionInfo(
		functionId,
		nameId,
		currentPc, // Store the function body's PC - 1 which coincidently is the current PC.
		minArgCount,
		maxArgCount,
		functionType,
		classId,
		localsCount,
		pcOffsetsForOptionalArgs));

	// This is SUPER tacky.
	// TODO: Do this name comparison at compile time and then stick that in the arguments which will also eliminate the
	// need for the obfuscation prefix since the namespace can be checked.
	if (nameId >= 0) {
		@type('string') name = $_array_get(ProgramData$p.identifiers, nameId);
		if ($_string_equals("_LIB_CORE_list_filter", name)) {
			$_array_set(ProgramData$p.primitiveMethodFunctionIdFallbackLookup, 0, functionId);
		} else if ($_string_equals("_LIB_CORE_list_map", name)) {
			$_array_set(ProgramData$p.primitiveMethodFunctionIdFallbackLookup, 1, functionId);
		} else if ($_string_equals("_LIB_CORE_list_sort_by_key", name)) {
			$_array_set(ProgramData$p.primitiveMethodFunctionIdFallbackLookup, 2, functionId);
		} else if ($_string_equals("_LIB_CORE_invoke", name)) {
			$_array_set(ProgramData$p.primitiveMethodFunctionIdFallbackLookup, 3, functionId);
		} else if ($_string_equals("_LIB_CORE_generateException", name)) {
			@type('MagicNumbers') mn = ProgramData$p.magicNumbers;
			MagicNumbers$mn.coreGenerateExceptionFunctionId = functionId;
		}
	}

	return pcJump;
}

function @type('Array<FunctionInfo>') getFunctionTable(@type('ProgramData') p, @type('int') functionId) {
	@type('Array<FunctionInfo>') oldTable = ProgramData$p.functionTable;
	@type('int') oldLength = $_array_length(oldTable);

	if (functionId < oldLength) return oldTable;

	@type('int') newLength = oldLength * 2;
	if (functionId >= newLength) newLength = functionId + 100;

	@type('Array<FunctionInfo>') newTable = $_new_array('FunctionInfo', newLength);
	for (@type('int') i = 0; i < oldLength; i += 1) {
		$_array_set(newTable, i, $_array_get(oldTable, i));
	}

	ProgramData$p.functionTable = newTable;
	return newTable;
}

/*
	Note that static methods do not appear anywhere in the class metadata as they all get (mostly) converted
	into normal function calls (annotated with the class ID to trigger static initializers).

	args[0] -> class ID
	args[1] -> base class ID or -1 if no parent
	args[2] -> global name ID for class (potentially not unique)
	args[3] -> constructor function ID
	args[4] -> static constructor function ID or -1
	args[5] -> number of static fields
	args[n .. n+5] -> fields and methods that are declared in this class.
		n + 0 -> 0 if field, 1 if method
		n + 1 -> member ID (numbering starts after base class member ID's
		n + 2 -> global name ID
		for fields:
			n + 3 -> initialization instruction
				0: use a literal
				1: use an empty list
				2: use an empty dictionary
			n + 4 -> literal ID or ignored
		for methods:
			n + 3 -> function ID
			n + 4 -> not used
*/
function @type('int') initializeClass(@type('int') pc, @type('ProgramData') p, @type('Array<int>') args, @type('string') className) {
	@type('int') i = 0;
	@type('int') memberId = 0;
	@type('int') globalId = 0;
	@type('int') functionId = 0;
	@type('int') t = 0;

	@type('int') classId = $_array_get(args, 0);
	@type('int') baseClassId = $_array_get(args, 1);
	@type('int') globalNameId = $_array_get(args, 2);
	@type('int') constructorFunctionId = $_array_get(args, 3);
	@type('int') staticConstructorFunctionId = $_array_get(args, 4);
	@type('int') staticInitializationState = 0;
	if (staticConstructorFunctionId == -1) {
		staticInitializationState = 2;
	}
	@type('int') staticFieldCount = $_array_get(args, 5); // just static fields. no static methods. those function IDs are resolved inline at compile time

	@type('Array<Value>') staticFields = $_new_array('Value', staticFieldCount);
	for (i = 0; i < staticFieldCount; i += 1) {
		$_array_set(staticFields, i, VALUE_NULL);
	}

	@type('ClassInfo') classInfo = new ClassInfo(
		classId,
		globalNameId,
		baseClassId,
		staticInitializationState,
		staticFields,
		staticConstructorFunctionId,
		constructorFunctionId,
		0, // member count
		null, // Array of member ID -> function IDs (with -1 fallback)
		null, // Field initialization command by member ID
		null, // Literal starting value by member ID
		null, // Global nameID -> member ID
		className); // fully qualified class name

	@type('Array<ClassInfo>') classTable = getClassTable(p, classId);

	$_array_set(classTable, classId, classInfo);

	@type('List<ClassInfo>') classChain = $_new_list('ClassInfo');
	$_list_push(classChain, classInfo);
	@type('int') classIdWalker = baseClassId;
	while (classIdWalker != -1) {
		@type('ClassInfo') walkerClass = $_array_get(classTable, classIdWalker);
		$_list_push(classChain, walkerClass);
		classIdWalker = ClassInfo$walkerClass.baseClassId;
	}

	@type('ClassInfo') baseClass = null;
	if (baseClassId != -1) {
		baseClass = $_list_get(classChain, 1);
	}

	// populate all the members. basically just copy the member lookups for the base class and then
	// overlay the current classes value on top of it.
	// Note that the current class may re-define some methods with the same member ID as a parent class.

	// initialize members
	@type('List<int>') functionIds = $_new_list('int');
	@type('List<int>') fieldInitializationCommand = $_new_list('int');
	@type('List<Value>') fieldInitializationLiteral = $_new_list('Value');
	@type('Dictionary<int, int>') globalNameIdToMemberId = $_new_dictionary('int', 'int');

	// copy from base class
	if (baseClass != null) {
		for (i = 0; i < ClassInfo$baseClass.memberCount; i += 1) {
			$_list_push(functionIds, $_array_get(ClassInfo$baseClass.functionIds, i));
			$_list_push(fieldInitializationCommand, $_array_get(ClassInfo$baseClass.fieldInitializationCommand, i));
			$_list_push(fieldInitializationLiteral, $_array_get(ClassInfo$baseClass.fieldInitializationLiteral, i));
		}

		@type('Array<int>') keys = $_dictionary_get_keys('int', ClassInfo$baseClass.globalIdToMemberId);
		for (i = 0; i < $_array_length(keys); i += 1) {
			t = $_array_get(keys, i);
			$_dictionary_set(globalNameIdToMemberId, t, $_dictionary_get_guaranteed(ClassInfo$baseClass.globalIdToMemberId, t));
		}
	}

	// and now add the overrides and appends
	for (i = 6; i < $_array_length(args); i += 5) {
		memberId = $_array_get(args, i + 1);
		globalId = $_array_get(args, i + 2);
		while (memberId >= $_list_length(functionIds)) {
			$_list_push(functionIds, -1);
			$_list_push(fieldInitializationCommand, -1);
			$_list_push(fieldInitializationLiteral, null);
		}
		$_dictionary_set(globalNameIdToMemberId, globalId, memberId); // potentially override previous entries
		if ($_array_get(args, i) == 0) { // field
			$_list_set(fieldInitializationCommand, memberId, $_array_get(args, i + 3));
			t = $_array_get(args, i + 4); // literal ID or -1 for NULL
			if (t == -1) {
				$_list_set(fieldInitializationLiteral, memberId, VALUE_NULL);
			} else {
				$_list_set(fieldInitializationLiteral, memberId, $_array_get(ProgramData$p.literalTable, t));
			}
		} else { // method
			functionId = $_array_get(args, i + 3);
			$_list_set(functionIds, memberId, functionId);
		}
	}

	ClassInfo$classInfo.functionIds = $_convert_list_to_array('int', functionIds);
	ClassInfo$classInfo.fieldInitializationCommand = $_convert_list_to_array('int', fieldInitializationCommand);
	ClassInfo$classInfo.fieldInitializationLiteral = $_convert_list_to_array('Value', fieldInitializationLiteral);
	ClassInfo$classInfo.memberCount = $_list_length(functionIds);
	ClassInfo$classInfo.globalIdToMemberId = globalNameIdToMemberId;

	if ($_string_equals("Core.Exception", className)) {
		@type('MagicNumbers') mn = ProgramData$p.magicNumbers;
		MagicNumbers$mn.coreExceptionClassId = classId;
	}
	return 0;
}

function @type('Array<ClassInfo>') getClassTable(@type('ProgramData') p, @type('int') classId) {
	@type('Array<ClassInfo>') oldTable = ProgramData$p.classTable;
	@type('int') oldLength = $_array_length(oldTable);
	if (classId < oldLength) return oldTable;
	@type('int') newLength = oldLength * 2;
	if (classId >= newLength) {
		newLength = classId + 100;
	}

	@type('Array<ClassInfo>') newTable = $_new_array('ClassInfo', newLength);
	for (@type('int') i = oldLength - 1; i >= 0; i -= 1) {
		$_array_set(newTable, i, $_array_get(oldTable, i));
	}

	ProgramData$p.classTable = newTable;
	return newTable;
}

function @type('int') resetLocalsStackTokens(@type('ExecutionContext') ec, @type('StackFrame') stack) {

	@type('Array<Value>') localsStack = ExecutionContext$ec.localsStack;
	@type('Array<int>') localsStackSet = ExecutionContext$ec.localsStackSet;

	@type('int') i = StackFrame$stack.localsStackOffsetEnd;
	while (i < $_array_length(localsStackSet)) {
		$_array_set(localsStackSet, i, 0);
		$_array_set(localsStack, i, null);
		i += 1;
	}

	@type('StackFrame') stackWalker = stack;

	while (stackWalker != null) {
		@type('int') token = StackFrame$stackWalker.localsStackSetToken;
		StackFrame$stackWalker.localsStackSetToken = 1;
		for (i = StackFrame$stackWalker.localsStackOffset; i < StackFrame$stackWalker.localsStackOffsetEnd; i += 1) {
			if ($_array_get(localsStackSet, i) == token) {
				$_array_set(localsStackSet, i, 1);
			} else {
				$_array_set(localsStackSet, i, 0);
				$_array_set(localsStack, i, null);
			}
		}
		stackWalker = StackFrame$stackWalker.previous;
	}

	ExecutionContext$ec.localsStackSetToken = 1;

	return -1;
}

function @type('int') increaseLocalsStackCapacity(@type('ExecutionContext') ec, @type('int') newScopeSize) {
	@type('Array<Value>') oldLocals = ExecutionContext$ec.localsStack;
	@type('Array<int>') oldSetIndicator = ExecutionContext$ec.localsStackSet;

	@type('int') oldCapacity = $_array_length(oldLocals);
	@type('int') newCapacity = oldCapacity * 2 + newScopeSize;

	@type('Array<Value>') newLocals = $_new_array('Value', newCapacity);
	@type('Array<int>') newSetIndicator = $_new_array('int', newCapacity);

	for (@type('int') i = 0; i < oldCapacity; i += 1) {
		$_array_set(newLocals, i, $_array_get(oldLocals, i));
		$_array_set(newSetIndicator, i, $_array_get(oldSetIndicator, i));
	}

	ExecutionContext$ec.localsStack = newLocals;
	ExecutionContext$ec.localsStackSet = newSetIndicator;

	return 0;
}

function @type('StackFrame') maybeInvokeStaticConstructor(
		@type('ProgramData') p,
		@type('ExecutionContext') ec,
		@type('StackFrame') stack,
		@type('ClassInfo') classInfo,
		@type('int') valueStackSize,
		@type('Array<int>') intOutParam) {

	$_array_set(intOutParam, 0, 0);
	@type('int') classId = ClassInfo$classInfo.id;
	if (ClassInfo$classInfo.staticInitializationState == 1) {
		// check to make sure it's the last item on the static init stack
		@type('List<int>') classIdsBeingInitialized = ProgramData$p.classStaticInitializationStack;
		if ($_list_get(classIdsBeingInitialized, $_list_length(classIdsBeingInitialized) - 1) != classId) {
			$_array_set(intOutParam, 0, 1);
		}
		return null;
	}

	ClassInfo$classInfo.staticInitializationState = 1;
	$_list_push(ProgramData$p.classStaticInitializationStack, classId);

	@type('FunctionInfo') functionInfo = $_array_get(
		ProgramData$p.functionTable,
		ClassInfo$classInfo.staticConstructorFunctionId);

	StackFrame$stack.pc -= 1; // when static constructor completes, return to this same op.

	@type('int') newFrameLocalsSize = FunctionInfo$functionInfo.localsSize;
	@type('int') currentFrameLocalsEnd = StackFrame$stack.localsStackOffsetEnd;
	if ($_array_length(ExecutionContext$ec.localsStack) <= currentFrameLocalsEnd + newFrameLocalsSize) {
		increaseLocalsStackCapacity(ec, newFrameLocalsSize);
	}

	if (ExecutionContext$ec.localsStackSetToken > 2000000000) {
		resetLocalsStackTokens(ec, stack);
	}

	ExecutionContext$ec.localsStackSetToken += 1;

	return new StackFrame(
		FunctionInfo$functionInfo.pc,
		ExecutionContext$ec.localsStackSetToken,
		currentFrameLocalsEnd,
		currentFrameLocalsEnd + newFrameLocalsSize,
		stack,
		false, // return value used?
		null, // value of 'this' keyword.
		valueStackSize,
		classId, // mark class as initialized when RETURN runs.
		StackFrame$stack.depth + 1,
		PostFinallyBehavior.NOTHING,
		null); // temp return value
}

/*
	There are 3n args that correspond to the n ESF tokens.

	3n + 0: PC of the beginning of the try block
	3n + 1: PC of the beginning of the exception sorting code
	3n + 2: PC of the finally block

	Final ESF data should be a per-row list of 2 values OR null if there is no ESF data:
	[pc][0] -> pc of exception sorter
	[pc][1] -> pc of finally
*/
function @type('Array<Array<int>>') generateEsfData(@type('int') byteCodeLength, @type('Array<int>') esfArgs) {
	@type('Array<Array<int>>') output = $_new_array('Array<int>', byteCodeLength);

	@type('List<Array<int>>') esfTokenStack = $_new_list('Array<int>');
	@type('Array<int>') esfTokenStackTop = null;
	@type('int') esfArgIterator = 0;
	@type('int') esfArgLength = $_array_length(esfArgs);
	@type('int') j = 0;

	for (@type('int') pc = 0; pc < byteCodeLength; pc += 1) {
		// Check to see if a new ESF arg needs to be pushed onto the stack.
		if (esfArgIterator < esfArgLength && pc == $_array_get(esfArgs, esfArgIterator)) {
			// push the next esf arg onto the stack.
			esfTokenStackTop = $_new_array('int', 2);
			for (j = 1; j < 3; j += 1) {
				$_array_set(esfTokenStackTop, j - 1, $_array_get(esfArgs, esfArgIterator + j));
			}
			$_list_push(esfTokenStack, esfTokenStackTop);
			esfArgIterator += 3;
		}

		// Check to see if the top of the ESF stack needs to be popped
		while (esfTokenStackTop != null && $_array_get(esfTokenStackTop, 1) <= pc) {
			$_list_pop(esfTokenStack);
			if ($_list_length(esfTokenStack) == 0) {
				esfTokenStackTop = null;
			} else {
				esfTokenStackTop = $_list_get(esfTokenStack, $_list_length(esfTokenStack) - 1);
			}
		}

		// Set the stack top to the current row
		$_array_set(output, pc, esfTokenStackTop);
	}

	return output;
}

function @type('int') markClassAsInitialized(@type('StackFrame') stack, @type('int') classId) {
	@type('ProgramData') p = $_get_program_data();
	@type('ClassInfo') classInfo = $_array_get(ProgramData$p.classTable, StackFrame$stack.markClassAsInitialized);
	ClassInfo$classInfo.staticInitializationState = 2; // mark class as completely ready to use.
	$_list_pop(ProgramData$p.classStaticInitializationStack);
	return 0;
}
