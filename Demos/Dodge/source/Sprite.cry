import Math;

class Sprite {

    field type;
    field x;
    field y;
    field targetX;
    field targetY;
    field dx = 0;
    field dy = 0;
    field dead = false;
    field lastDir = 's';
    field v = 4;
    field moving = false;

    constructor(type, x, y) {
        this.type = type;
        this.x = x;
        this.y = y;
    }

    function updateDirection(dx, dy) {
        angle = -Math.arctan(dy, dx);
        angle = (angle * .99) % (2 * Math.PI);
        if (angle < Math.PI / 4 || angle > 7 * Math.PI / 4) {
            this.lastDir = 'e';
        } else if (angle < 3 * Math.PI / 4) {
            this.lastDir = 'n';
        } else if (angle < 5 * Math.PI / 4) {
            this.lastDir = 'w';
        } else {
            this.lastDir = 's';
        }
    }

    function update() {
        if (this.type == 'fire' && this.targetX == null) {
            xy = randomEdgePoint();
            this.targetX = xy[0];
            this.targetY = xy[1];
        }

        if (this.targetX != null) {
            this.moving = true;
            dx = this.targetX - this.x;
            dy = this.targetY - this.y;
            distance = (dx ** 2 + dy ** 2) ** .5;
            if (distance < this.v) {
                this.x = this.targetX;
                this.y = this.targetY;
                this.targetX = null;
                this.targetY = null;
            } else {
                ratio = this.v / distance;
                dx *= ratio;
                dy *= ratio;
                this.x += dx;
                this.y += dy;
            }

            if (this.type == 'player') {
                this.updateDirection(dx, dy);
            }
        } else {
            this.moving = false;
        }
    }
}
