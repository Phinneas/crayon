import Math;

class PlayScene : AbstractScene {

    field player;
    field sprites;
    field counter = 0;
    field resetCounter = -1;
    field fires = 0;

    constructor() : base() {
        this.player = new Sprite('player', SCREEN_WIDTH / 2, SCREEN_HEIGHT / 2);
        this.sprites = [this.player];
    }

    function processInput(events) {
        for (e : events) {
            if (e.type == EventType.MOUSE_LEFT_DOWN) {
                x = Math.ensureRange(e.x, 20, SCREEN_WIDTH - 20);
                y = Math.ensureRange(e.y, 20, SCREEN_HEIGHT - 20);
                this.player.targetX = x;
                this.player.targetY = y;
            }
        }
    }

    function update() {
        this.counter++;
        this.resetCounter--;

        if (this.resetCounter == 0) {
            this.next = new PlayScene();
        }

        if (this.player.dead && this.resetCounter < 0) {
            this.resetCounter = 50;
            this.sprites.add(new Sprite('body', this.player.x, this.player.y));
        }

        if (this.counter % 20 == 0) {
            pt = randomEdgePoint();
            fire = new Sprite('fire', pt[0], pt[1]);
            pt = randomEdgePoint();
            fire.targetX = pt[0];
            fire.targetY = pt[1];
            this.sprites.add(fire);
            if (this.resetCounter < 0) {
                this.fires++;
            }
        }

        px = this.player.x;
        py = this.player.y;

        newSprites = [];
        for (i = 0; i < this.sprites.length; ++i) {
            sprite = this.sprites[i];
            sprite.update();
            if (!sprite.dead) {
                newSprites.add(sprite);
            }

            if (sprite.type == 'fire') {
                dx = px - sprite.x;
                dy = py - sprite.y;
                if (dx ** 2 + dy ** 2 < 20 ** 2) {
                    this.player.dead = true;
                }
            }
        }
        this.sprites = newSprites;

    }

    function render() {
        for (sprite : this.sprites) {
            if (sprite.type == 'player') {
                frame = 1;
                if (sprite.moving) {
                    frame = (this.counter / 3) % 4;
                    frame += 1;
                    if (frame == 4) {
                        frame = 2;
                    }
                }
                img = ImageLibrary.get('player/' + sprite.lastDir + frame);
            } else if (sprite.type == 'body') {
                img = ImageLibrary.get('player/dead');
            } else {
                img = ImageLibrary.get('fire' + ((this.counter / 3) % 2 + 1));
            }
            x = floor(sprite.x - img.width / 2);
            y = floor(sprite.y - img.height / 2);
            img.draw(x, y);
        }

        x = 10;
        y = SCREEN_HEIGHT - ImageLibrary.get('numbers/num0').height - 10;
        for (c : '' + this.fires) {
            img = ImageLibrary.get('numbers/num' + c);
            img.draw(x, y);
            x += img.width + 5;
        }
    }
}
