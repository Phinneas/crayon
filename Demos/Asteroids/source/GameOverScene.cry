class GameOverScene : AbstractScene {
	constructor(bg) : base() {
		this.bg = bg;
		this.counter = 0;
		
		this.game_over = IMAGE_LIBRARY.get_image('game_over.png');
		this.press_start = IMAGE_LIBRARY.get_image('press_start.png');
		
	}
	
	function update(events, pressed) {
		this.counter++;
		restart = false;
		for (event : events) {
			if (event.is_key && event.down) {
				if (event.key == 'space' || event.key == 'enter') {
					if (this.counter > 10) { // don't let screen go away if player is still shooting frantically
						restart = true;
					}
				}
			}
		}
		
		this.bg.update(events, pressed);
		
		if (restart) {
			this.next = new PlayScene(false);
		}
	}
	
	function render(rc) {
		this.bg.render(rc);
		$gfx_draw_rectangle(0, 0, GAME_WIDTH, GAME_HEIGHT, 0, 0, 0, 128);
		
		game_over_x = GAME_WIDTH / 2 - this.game_over.width / 2;
		game_over_y = GAME_HEIGHT / 4 - this.game_over.height / 2;
		press_start_x = GAME_WIDTH / 2 - this.press_start.width / 2;
		press_start_y = GAME_HEIGHT / 4 - this.press_start.height / 2 + GAME_HEIGHT / 2;
		
		$gfx_blit_image(this.game_over, game_over_x, game_over_y);
		if ((this.counter / 10) % 2 == 0) {
			$gfx_blit_image(this.press_start, press_start_x, press_start_y);
		}
		
	}
}