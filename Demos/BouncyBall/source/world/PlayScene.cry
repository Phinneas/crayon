class PlayScene : AbstractScene {
	constructor() : base() {
		this.sky = IMAGE_LIBRARY.get('sky.png');
		
		this.map = new Map('level1');
		this.sprites = [];
		start_x = this.map.start_loc[0] - .5;
		start_y = this.map.start_loc[1] - .5;
		this.player = new Sprite('player', start_x, start_y);
		this.sprites.add(this.player);
		cx = this.player.x * 32;
		cy = this.player.y * 32;
		this.camera_target_x = cx;
		this.camera_target_y = cy;
		this.camera_current_x = cx;
		this.camera_current_y = cy;
	}
	
	function update(events, pressed_keys) {
		v = .2;
		move_right = pressed_keys['right'];
		move_left = pressed_keys['left'];
		move = move_right || move_left;
		
		
		for (event : events) {
			if (event.action == 'jump' && event.down && this.player.ground != null) {
				this.player.ground = null;
				this.player.vy = -.6;
			}
		}
		
		if (move) {
			this.player.accelerate_x(move_right);
		}
		
		for (sprite : this.sprites) {
			sprite.update(this);
		}
	}
	
	function render(rc) {
		
		this.camera_target_x = this.player.x * 32;
		this.camera_target_y = this.player.y * 32;
		this.camera_current_x = (this.camera_current_x * 5 + this.camera_target_x) / 6.0;
		this.camera_current_y = (this.camera_current_y * 5 + this.camera_target_y) / 6.0;
		view_x = $floor(this.camera_current_x) - WIDTH / 2;
		view_y = $floor(this.camera_current_y) - HEIGHT / 2;
		if (view_x < 0) view_x = 0;
		if (view_y < 0) view_y = 0;
		map_width = this.map.width * 32;
		map_height = this.map.height * 32;
		if (view_x > map_width - WIDTH) view_x = map_width - WIDTH;
		if (view_y > map_height - HEIGHT) view_y = map_height - HEIGHT;
		
		
		$gfx_fill_screen(255, 0, 0);
		$gfx_blit_image(this.sky, 0, 0);
		
		this.map.render(rc, view_x, view_y);
		
		for (sprite : this.sprites) {
			sprite.render(rc, view_x, view_y);
		}
	}
}